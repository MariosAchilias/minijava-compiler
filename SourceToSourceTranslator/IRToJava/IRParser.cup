package SourceToSourceTranslator.IRToJava;
/* Simple +/-/* expression language; parser evaluates constant expressions on the fly*/
/**
 *  Package and Import Specifications
 */
import java_cup.runtime.*;

/**
 *  Usercode Components
 */
parser code {:
    // Connect this parser to a scanner!
    Lexer s;
    Parser(Lexer s){ this.s=s; }
:}

/* define how to connect to the scanner! */
scan with {: return s.next_token(); :};

/**
 *  Symbol Lists
 */

/* Terminals (tokens returned by the scanner). */
terminal            PLUS, IF, ELSE, REVERSE, LPAREN, RPAREN, LBRACK, RBRACK, RPARENLBRACK, COMMA, PREFIX;
terminal String     ID, STRING_LITERAL;

/* Unused tokens */
terminal            EQUALS, SUFFIX;

/*  Non terminals */
non terminal           statement_list, program, decl_func, function_declarations, decl_arg_list, call_arg_list, condition;
non terminal String    if_else, func_call, expr, declaration;
/**
 *  Precedence Declarations
 */
precedence left REVERSE;
precedence left IF, ELSE;
precedence left PLUS;

/* Solve conflict between expressions and declaration arguments */
precedence left COMMA;
precedence left expr;

/**
 *  The Grammar Rules
 */

program                 ::= function_declarations:declarations statement_list:statements        {: System.out.println("public class Translated {\n\t public static void main(String[] args) {\n" + statements + "\t}\n"); System.out.println(declarations); System.out.println("}"); :}
                        | function_declarations:declarations                                    {: System.out.println("public class Translated {\n\t public static void main(String[] args) {\n\t}\n"); System.out.println(declarations); System.out.println("}"); :}
                        | statement_list:statements                                             {: System.out.println("public class Translated {\n\t public static void main(String[] args) {\n" + statements + "\t}\n}"); :}
;
function_declarations   ::= function_declarations:list declaration:d                            {: RESULT = list + d;:}
                        |  declaration:d                                                        {: RESULT = d; :}
;
declaration             ::= decl_func:decl expr:e RBRACK                                        {: RESULT = "\tpublic static String " + decl + " {\n\t\treturn " + e + ";\n\t}\n\n"; :}
;
decl_func               ::= ID:name LPAREN decl_arg_list:arguments ID:last_arg RPARENLBRACK                  {: RESULT = name + "(" + arguments + ", String "+ last_arg + ")"; :}
                        |   ID:name LPAREN RPARENLBRACK                                          {: RESULT = name +"()"; :}
                        |   ID:name LPAREN ID:arg RPARENLBRACK                                          {: RESULT = name+"(String "+arg+")"; :}

;
decl_arg_list           ::= decl_arg_list:list ID:id COMMA                                  {: RESULT = list + ", String " + id; :}
                        |   ID:id COMMA                                                     {: RESULT = "String " + id; :}
;
statement_list          ::= statement_list:list expr:e                                 {: RESULT = list + "\t\tSystem.out.println(" + e + ");\n"; :}
                        | expr:e                                                         {: RESULT = "\t\tSystem.out.println(" + e + ");\n"; :}
;
func_call               ::= ID:func LPAREN call_arg_list:arguments RPAREN                        {: RESULT = func + "(" + arguments + ")"; :}
                        | ID:func LPAREN RPAREN                                                       {: RESULT = func + "()"; :}
;
call_arg_list           ::= call_arg_list:list COMMA expr:e                                 {: RESULT = list + ", " + e; :}
                        |   expr:e                                                          {: RESULT = e; :}
;
if_else                 ::= IF LPAREN condition:cond RPAREN expr:e1 ELSE expr:e2               {: RESULT = "(("+cond+") ? "+e1+" : "+e2+")"; :}
;
condition               ::= expr:e1 PREFIX expr:e2                                          {: RESULT = e2+".startsWith("+e1+")"; :}
;
expr                    ::= expr:e1 PLUS expr:e2                                            {: RESULT = e1 + "+" + e2; :}
                        | STRING_LITERAL:s	                                            {: RESULT = "\"" + s + "\"";:}
                        | func_call:call                                                    {: RESULT = call; :}
                        | if_else:ternary                                                   {: RESULT = ternary; :}
                        | REVERSE expr:e                                                    {: RESULT = "((new StringBuilder("+e+")).reverse().toString())"; :}
                        | ID:id                                                             {: RESULT = id; :}
;
